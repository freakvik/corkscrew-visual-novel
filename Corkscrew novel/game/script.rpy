#characters
define cork = Character("Штопор", color="#bd2323", image="cork") #main character
define cashier = Character("Кассирша", color="#d39323", image='cashier') #for start
define bt = Character("Барменша", color="#2EAFCE", image="bartender") #for pub
define man = Character("Мужик", color="#bfd628", image='man') #for pub
define speaker = Character("Диктор", color="#ffffff") #for concert
define bear = Character("Член группировки \"Медведи\"", color="#03953e", image="bear") #for concert
define col = Character("Бывшая коллега", color="#6d1e02", image="collegue") #for bath
define old_ = Character("Старик", color="#e310cb", image="old") #for room

define info = False #knowledge about generator
define details = False #bartender knowledge about generator 
define positive = False #the way to positive ending from pub

#music and sounds
define audio.menu_ = "main-menu-theme.mp3"
define audio.normal = "music/music1.mp3"
define audio.battle = "music/music2.mp3"
define audio.boom = "sounds/boom.ogg"
define audio.gun = "sounds/gun.ogg"
define audio.punch = "sounds/punch.ogg"

image menu_slideshow:
    "gui/menu/slide (1).png" 
    pause 10.0
    "gui/menu/slide (2).png"
    pause 10.0
    "gui/menu/slide (3).png"
    pause 10.0
    "gui/menu/slide (4).png"
    pause 10.0
    "gui/menu/slide (5).png"
    pause 10.0
    "gui/menu/slide (6).png"
    pause 10.0
    repeat



init: #change coordinates for sprites:
    $ right2 = Position(xalign=0.9, yalign=-0.3)
    $ left2 = Position(xalign=0.1, yalign=0.0)

#game start
label start:

    scene bg house
    play music normal fadein 1.5

    "Вы просыпаетесь одни в своём доме в объятиях сильной головной боли и ощущения сухости во всём теле."
    "Рядом валяется бесчисленное множество пустых бутылок из-под алкоголя, преимущественно водки."
    "Ваше имя – Штопор, хотя бы это вы помните отчётливо, будто это ввинчено в голову."

    show cork drunk at right2 with dissolve

    cork "Агрх… ну, здравствуй, жизнь, давно не виделись. Правда, после такого, не думал, что мы ещё увидимся."
    cork "Башка трещит мама не горюй. Так и откинуться недолго. Эх, пивка бы сейчас…"

    hide cork drunk with easeoutright

    "Вы уходите на кухню, чтобы посмотреть наличие пива в холодильнике. Пива, там, к сожалению, не оказывается, как и в принципе любой жидкости, подходящей для питья."
    "Что, из-под крана? Сразу игру хочешь закончить, да? А бутылки, валяющиеся рядом, громко звенят о том, что они точно пустые."

    show cork unhappy at right2 with easeinright

    cork "Чёрт, что ж, придётся пилить в ближайший ларёк. Надеюсь, деньги то у меня хотя бы есть после такого вечера!"

    scene bg shop with fade

    "Через пару минут шаткой походки по полупустым улицам вы наконец-то оказываетесь в ларьке, где можно купить много всякого полезного для развития рака, в том числе и известное средство против похмелья."
    "(Хотя эффективность которого не доказана)"
    "По вашему виду можно отчётливо сказать, чем именно вы сейчас страдаете, попутно пытаясь вытащить всю мелочь из карманов."

    show cork neutral at right2 with easeinright

    cork "Эй, детка! Мне бы холодненького пару баночек, молю."

    show cashier unhappy at left2 with easeinleft

    cashier "A-ага, c-щас! Разбежался! Держи карман шире. Обождёшь, алкаш несчастный."
    cork "Ну-у, мадам, что такое. Неужели я не могу в свой последний час насладиться последней баночкой пива?"
    cashier "А нечо было бухать как проклятый! Тьфу на всех вас, чтоб вам пусто было."

    hide cork neutral with dissolve
    hide cashier unhappy with dissolve

    "Может даже если в голове у вас и было пусто, даже никаких опилок, в руках лежало несколько помятых купюр."
    "Вы резким уверенным движением руки прихлопнули эти несчастные бумажки к прилавку, заглядывая в недовольные глаза кассирши."
    
    show cork neutral at right2 with dissolve
    
    cork "{b}Мне пива. Холодного.{/b}"
    "Ваш голос звучал настолько уверенно, что этой уверенности хватило бы на целую Госдуму."
    
    show cashier neutral at left2 with dissolve

    cashier "Да откуда я тебе его возьму, ирод? Элестричества уже какой час нет!"
    cork puzzled "Как это нет элест... электричества?"
    cashier "А вот! С ночи самой тока ентого нет, такая же алкашня, как и ты провода медные побежала резать."
    cashier "Доворовались, ага, одного увезли обгорелого, и поделом. И ты ещё тут припёрся за своим пойлом."
    cork neutral "Так, подожди, подожди. Ну, ладно, питание отрубили, так должен же быть тут какой-то генератор, чтоб не портилось ничего."
    cashier unhappy "Тю-ю! Куда там. Охранник ентот, что сидел неподалёку, говорит, будто громыхнуло тут что ночью. А проверять не полез, бездельник!"
    cork "И что теперь?"
    cashier "Что-что. Вот, не работает ни шиша! Вся продукция портится лежит, а никто и в ус не дует. Все заняты, видите ли. Ну да, ну да, лучше ведь по подворотням ползать."
    cork puzzled "Так это что получается, теперь всё тухнет лежит?"
    cashier neutral "Ой, ну, наконец-то до него дошло, а, вы посмотрите, какое чудо! Вот именно. И пиво твоё несчастное тоже тёплое лежит."
    cork unhappy "Нет уж, спасибо, тёплого мне не надо. Если бы я хотел блевануть, я б ещё раз водкой затарился."
    cashier "Ну так ступай тогда отсюды, раз ничо не хочешь."
    cork puzzled "Не-е, мадам, потерпите меня тут ещё немного. А как его этот... генератор, его ещё раз нельзя запустить, что ли?"
    cashier unhappy "А я почём знаю? Сам сходи и проверь. У меня своих дел много, и так с тобой много проболтала тут. Всё, брысь отсюда! Делай, шо хошь."
    cork "Ну и ну... Придётся что-то делать."

    menu: 
        "Куда пойдёшь?"
        "Сходить проверить генератор":
            cork neutral "Ладно, делать всё равно нечего, можно и посмотреть хотя бы одним глазком."
            jump generator
        "Выйти из ларька":
            hide cashier neutral with easeoutleft
            cork "И фиг с ним. Их проблемы, а меня пиво ждёт. Пойду в бар, там хоть что-то вкусное будет. И, возможно, горючее тоже."
            jump pub

    return

label generator:

    "Вы решили покинуть кассиршу и оставить её в гордом одиночестве, а самим отправиться в подвал магазина, чтобы посмотреть, что случилось с генератором."
    "Но перед этим предусмотрительно захватили обратно свою мелочь. Мало ли, пригодится."

    scene bg generator with fade

    "Вы заходите в тёмный сырой подвал небольшого магазина. Удивительно, что он тут вообще есть, но тут как раз и должен находиться генератор."
    "Вокруг всё пропахло палёной шерстью, стены и пол покрыты копотью от взрыва, а от самого генератора осталась лишь груда металлолома."
    "Здоровья погибшим. Видно, незавидная судьба была у него, раз взорваться решил."
    "Или кто-то решил его взорвать."

    show cork puzzled at right2 with easeinright

    cork "Чёрт возьми, тут что, собак жарили, боже..."
    cork "И какого чёрта он вообще взорвался? Не выдержал нагрузки, бедный? Н-да, слишком большое количество пива убивает не только людей, но и генераторы."
    show cork neutral at right2
    "Вы подходите ближе к самому генератору. Точнее, тому, что от него осталось. Рядом валяются разбросанные окурки. По их виду можно было сказать, что выкурены они были сравнительно недавно."
    "Поверьте на слово, вы очень опытны в этом плане. Глаз намётан, не раз приходилось видеть людей, которые курят сигареты пачками."
    "Так вот, из вида окурков следует, что кто-то тут недавно был и делал что-то очень подозрительное. Возможно, жёг собак заживо."
    "К сожалению, вы не судмедэксперт и не можете собрать больше улик, чем пару окурков, которые, к тому же, уже и не докурить, поэтому для вас это просто бесполезный хлам."
    "И эта шерсть ещё... Всё произошедшее здесь казалось странным и щекотало нервы, от такого и протрезветь можно."
    show cork puzzled at right2
    "Вы задумались о том, зачем вы сюда пришли и что конкретно искали, пока на целых 5 минут не залипли в одну точку, пытаясь напрячь оставшиеся нейроны в голове."
    "Как оказалось, нейроны в голове отсутствовали, как и диплом судмедэксперта, пришлось оставить всё это добро на месте и покинуть помещение."
    "Но как же генератор? Вот так его просто оставить на месте и ничего с ним не делать? Проводка вокруг намертво обгорела, пытаться чинить это всё гиблое дело."
    "Никаких дополнительных источников питания здесь предусмотрено не было. Весь бензин, что находился тут для запаса, как раз и взорвался, прихватив с собой на тот свет и генератор."
    "В помещении был стойкий запах гари и безнадёги. Всё, что могло сгореть сгорело и оставило после себя дым и кучки пепла, а что гореть не могло развалилось на части."
    "Всё равно с генератором уже ничего не сделаешь. Правда, можно было бы попробовать сдать его на металлолом, однако пронести всю эту груду металла мимо кассирши казалось невозможным."
    "Вы оставили в стороне все глупые назойливые идеи и наконец-то вышли из подвала, наслаждаясь относительно свежим воздухом. Запах жжёной шерсти сменился на затхлый запах маленького магазина."
    hide cork neutral with easeoutright
    $ info = True
    #knowledge about the destroyed generator
    "Что ж, пора двигаться в бар."
    jump pub
    return

label pub:

    hide cork neutral with easeoutright
    scene bg pub with fade

    "Вы заходите в местный бар, который проще будет назвать просто пивнушкой, чем каким-то серьёзным заведением."
    "Сразу со входа в нос бьёт резкий запах спирта, смешанный из множества выпитых до дна бутылок, но со временем к этому можно привыкнуть, как и к перегару, особенно если напиться твоя цель."
    "Глаза быстро привыкают к полумраку, в котором ярко сияют разноцветные лампы, погружая в расслабленную и слегка весёлую атмосферу. Свет отражается от стекла и играет бликами в напитках."
    "Мешанина из звуков не кажется такой громкой, пусть рядом постоянно и происходят какие-то потасовки и слышна ругань, смысл которой тонет в музыке, исходящей из колонок под потолком."
    "И пусть в рот ещё не попала ни капля алкоголя, на языке уже мерещится едва ощутимая горечь спирта, спутанная со вкусом жирных закусок и, для любителей драк, вкусом крови."
    "Здесь собираются местные алкаши, дебоширы и просто любители заполнить свою внутреннюю пустоту, но у которых в то же время есть немного больше денег, чем на пару бутылок в месяц."
    "Внешний вид заведения не внушал особого доверия, ведь здесь не было даже охраны, однако никто не смел разрушать местную идилию, так как это был единственный бар на всё поселение."
    "Да и если заведение закроется, посёлок потеряет большую часть своего дохода, а местные выпивохи источник веселья."

    show cork puzzled at right2 with easeinright

    cork "Чёрт возьми... Вот же деньги дерут с простых людей! В магазине всё раза в два дешевле."
    
    show bartender neutral at left2 with easeinleft

    bt "Ну так и иди в магазин тогда, зачем здесь выделываешься. Если ничего заказывать не будешь проходи дальше, не задерживай меня."
    cork neutral "Ну, нет, я бы с радостью, но оттуда мне дали от ворот поворот."
    bt "Что, уже прикончил все запасы алкоголя там?"
    cork "Я бы с радостью, да только там всё электричество отрубило, как алкаша после 5 бутылок, а тёплое пиво я пить не стану, уж лучше от похмелья помереть."
    bt happy "Ну-ну, я об этом уже слышала от здешних посетителей. Не поверишь, у них такая же проблема. Ты уже.. седьмой по счёту."

    if info:

        cork "Да не только в этом дело! Там генератор бомбанул, да так, что от него только памятник остался. Или кто-то его бомбанул, не уверен..."
        bt "А вот про это я уже не знаю. Интересно-интересно, а что ты там ещё видел? И нафига к генератору полез?"
        cork puzzled "Да меня эта.. кассирша попросила. Ну, знаешь, сама пролезть не может, поэтому я туда и полез. Правда за это я всё равно ничего не получил, но было познавательно."
        bt happy "Ха-ха, знаю-знаю её. Не думаю, что она хоть когда-нибудь бы и пошла туда проверять. Но всё же, что там?"

        menu: 
            "Рассказать барменше подробности про генератор?"
            "Да, почему нет, это не так важно.":
                cork neutral "Там.. да ничего особого. Просто куча металлолома. И воняет ещё жутко так, будто собак заживо жарили."
                bt "Подожди-подожди, ты, вроде, говорил, что этот генератор сам взорвался. Или его кто-то взорвал, так?"
                cork "Так."
                bt "Интересно... но, хорошо, продолжай."
                cork "В общем, вот. Там ещё какой-то мусор валялся, типо бычков на земле, ничего особенного."
                bt "Да я бы не сказала, что ничего особенного. Странно-странно это всё."
                cork puzzled "Что ты имеешь в виду?"
                bt "Я? А-а.. я кое-что знаю, только это, м.."
                cork "Что?"
                bt "Нет, ничего. Тебе есть ещё что-нибудь рассказать?"
                cork neutral "Да, собственно, ничего кроме ларькового терроризма."
                $ positive = True #way to good ending

            "Нет, я ей не доверяю.":
                cork neutral "Да там... и правда ничего особенного. Просто взорвался и всё."
                bt "Просто так взорвался и всё?"
                cork "Ага, я так и сказал."
                bt "Н-да... Я думала будет что-то прям загадочное."
                cork "Хах, да что тут у нас происходит такого необычного. Самое удивительное событие, которое тут когда-либо происходило это когда мужик по пьяни утонул."
                cork happy "А с него потом ведро раков сняли."
                bt unhappy "А то я помню нам когда-то внезапно поступила партия раков... Думала ещё, что за запах мерзкий-мерзкий."
                cork neutral "Ауч, я просто пытался пошутить..."
                bt "Больше не пытайся, хорошо?"
                cork unhappy "Обидно."
                bt happy "Ага, знаю-знаю. Но ты не дуйся, всё равно смешнее от этого не станешь. Хотя выглядишь ты так забавно даже."
                cork "Я..."
                bt "Да вижу-вижу, как у тебя всё внутри рухнуло. Не переживай, я таких, как ты, каждый день вижу десятками, не такие уж вы и страшные."
                cork neutral "..."
                pause 1.5
                cork "Ладно."
    else:
        cork puzzled "Ого, видно, я не один такой разочарованный жизнью. А, вообще, что они сами-то говорили, все внезапно нашли пачку денег на дороге? Кассу ограбили?"
        bt neutral "Хах, если бы, если бы. Нет, говорят, что у магазина того конкретные перебои с электричеством, вообще ничего не работает. Как обычно, то ли медь кто срезал, то ли ещё что."
        bt "Но это уже абсолютно не моё дело. Спасибо, выручку сегодня побольше сделаю. Чаевые хоть оставят. Надеюсь."
        cork neutral "Готов оставить чаевые в крупном размере за то, чтобы вы вылечили мою голову. Раскалывается всё просто, ужасно."
        bt "И что ты предлагаешь? Для лечения головы тебе к психиатру, хотя не думаю, что даже он тебе поможет."
        cork puzzled "Ну зачем же так строго сразу. Мне бы чего-нибудь такого... выпить."
        bt happy "Яду?"
        cork neutral "Да нет же! Я про другое. Про спиртное, если быть точным."
        bt "Нет-нет, всё ещё недостаточно точно. Давай, ещё пара попыток, ты обязательно сможешь сказать это."
        cork puzzled "А-ага... Да... Точно... Сейчас"
        bt neutral "..."
        pause 1.5
        cork "Ещё секунду, я просто... плохо соображаю после вчерашнего, будто реально яду выпил."
        bt unhappy "Ну так это почти одно и то же, дурень. И то, и то тебя в могилу сведёт. Ты ещё керосин в себя залей, сразу как генератор заработаешь."
        cork happy "А что сразу генератор-то? Я, может, и так уже машина сам по себе."
        bt happy "Ну-ну, вижу, какой ''красивый'' автомобиль тут припарковался. Дно уже прогнило, отвалится щас."
        cork unhappy "Эй! Ты на что это намекаешь, женщина? Всё со мной в порядке, не надо на меня наговаривать."
        bt "Всё, боюсь-боюсь, только не бейте, дяденька."
        bt neutral "Слушай, а ты и правда про генератор ничего-ничего не знаешь, или просто бухло память стёрло?"
        cork neutral "А что я, собственно, должен знать?"
        bt "Ну.. хотя бы что-нибудь?"
        cork "Начнём с того, что это я должен спрашивать, что случилось с генератором, и где, вашу ж за ногу, этот генератор находится."
        bt "Ты ж из магазина сюда пришёл, верно?"
        cork "Да, абсолютно точно."
        bt "Тогда ты должен догадаться, где этот генератор находится."
        cork puzzled "То есть в самом магазине?"
        bt "Да-да."
        cork "Тогда почему электричества всё равно нет? Мне эта кассирша говорила, что генератор не работает, что-то там про охранника, ночью где-то бомбануло... Я так ничего и не понял."
        bt happy "Ха-ха, неудивительно. Для того, чтобы что-то понять, мозги нужны, а ты даже их пропить смог."
        cork "Но всё-таки, что с генератором-то?"
        bt unhappy "Ох, да сдался он тебе. Забудь про него, ты всё равно с ним уже ничего-ничего не сделаешь."
        cork "И что, не починить даже? Ремонт какой-то?"
        bt neutral "Ты ещё скажи, что у тебя инженерное образование."
        cork neutral "Ну-у.. нет, но я могу хотя бы посмотреть."
        bt "Ты б лучше в музей сходил. Посмотреть. Давай-давай, хватит время тянуть."


    cork neutral "Точно. Я ж забыл, за чем сюда пришёл."
    cork "Прошу, дайте две бутылки пива, любого."
    bt neutral "Хорошо. Только холодного у нас тоже нет, запретили продавать. Есть тёплое."
    cork "..."
    pause 1.5
    cork unhappy "...Да что ж за..."
    cork "Наверняка эти алкаши забрали всё себе раньше меня."
    stop music fadeout 1
    play music battle fadein 1
    hide bartender neutral with dissolve
    show man angry at left2 with moveinleft
    man "Хэй, идиот! Ты кого там алкашом назвал?"
    cork neutral "А тебя колышит? Сиди дальше и не высовывайся, не с тобой разговариваю."
    man "За слова свои ответь, иначе все зубы повышибаю!"
    cork "Ты себя то видел? Мужичок, иди отсюда, пока цел."
    man "За базар ответь, я тебе говорю! Или ты как баба?"
    cork unhappy "Подонок. Я тебя пополам переломаю, быстро свалил отсюда!"
    man "Ты кого алкашом назвал, я спрашиваю?! Ты думаешь я себя контролировать не умею?! Я тупой по-твоему?!"
    cork "Голос на меня свой не повышай, пискля! Сопли свои подобрал и заткнулся, пока кости целы!"
    man "Да ты кто вообще такой! Ты знаешь, кто я? Ты пальцем только тронь меня, тебя уроют просто!"
    cork "Да ты никто! И тупой к тому же!"
    man "Ах ты ж мразь! Ну всё, тебе конец! Сюда иди!"

    menu:
        "Ударить взбесившегося мужчину?"
        "Да, заслужил, подслушивать чужие разговоры это нехорошо":
            $ positive = True #way to good ending
            cork angry "Ну, давай-давай, подходи, тварь!"
            man "На том свете меня вспоминать будешь! Ты пожалеешь об этом!"
            cork "Заткнись!"
            stop music fadeout 1
            hide cork angry
            hide man angry 
            play sound punch
            show punch with hpunch
            pause 1.5
            hide punch with dissolve
            show cork unhappy at right2 with dissolve 
            cork "Вот пидорас."

        "Нет, я против бессмысленного насилия":
            cork "Полегче, ты куда прёшь-то?"
            man "Я тебе сейчас все зубы выбью!"
            cork "Тише, мужик, мне проблемы не нужны, угомонись!"
            man "Что, псина, хвост поджал?"
            cork "Иди отсюда уже, иначе полицию вызову!"
            man "Ха, напугал ежа голой жопой. Ты тряпка, сам за свои слова ответить не можешь!"
            man "Ладно, живи пока что. И не дай Бог ты мне снова на глаза попадёшься! Рёбер не досчитаешься."
            hide man angry with moveoutleft

    stop music fadeout 1
    play music normal fadein 1
    show cork neutral at right2 with dissolve
    cork "Фух, быстро же его сдуло."
    show bartender neutral at left2 with easeinleft
    bt "Ага, вроде того."
    cork "Я.. здесь есть ещё какие-то места в городе, где я могу взять себе выпить?"
    if positive:
        bt "Ты и правда надеешься, что я тебе просто так дам наводку на наших конкурентов, даже учитывая, что моего босса тут рядом нет?"
        bt "Ну-ну, и даже после того, что ты сделал... Ладно, скажу."
        bt "Есть ещё одно место, где ты можешь взять себе кое-что. Хотя я тоже не обещаю, что и там оно будет."
        cork "Ого, ну наконец-то, возможно, хотя бы сейчас повезёт, на третий-то раз."
        bt "Слушай-слушай. Где-то неподалёку собираются устраивать концерт. Или уже устроили, не знаю. Обычный такой рок, ничего особенного."
        bt happy "Громкая музыка, девушки, алкоголь, нарк... В общем всё, что ты так любишь."
        cork happy "Звучит весьма заманчиво! А где, где концерт-то проходит?"
        bt "Думаю, ты сможешь его найти и сам, заодно прогуляешься вокруг. Может, протрезвеешь."
        cork "Да я от дел таких и сам собой протрезвел уже. Но не прочь снова чего-нибудь выпить, раз повод есть!"
        bt neutral "Ну-ну, вам только повод дай."
        menu:
            "Ну что, граждане, погнали на концерт?"
            "Обязательно!":
                jump concert

            "А то!":
                jump concert
    else:
        bt unhappy "Ты серьёзно?"
        cork puzzled "А ну... почему нет? Просто спрашиваю."
        bt neutral "Нет-нет, я не алкаш, я понятия не имею, где здесь ещё можно спирт раздобыть. Босс где-то заказывает, но это уже из-за границы привозят."
        cork neutral "Охотно верю, что ж."
        bt "Ничем помочь не могу, увы. Работаю здесь по 12 часов, времени на пить не остаётся. Это дело только для безработных и ленивых."
        cork "Ха-ха, всё, понял уже, можешь не продолжать."
        bt happy "Что, опять по больному?"
        cork unhappy "Да почему опять-то? Ты бы лучше не несла такую чушь, а то..."
        bt "А то что? Что? Расстроишься и уйдёшь? Не бойся, мне потери среди клиентов не страшны, всё равно на следующий же день сюда снова приползёте."
        cork neutral "Ты бы лучше не бесила меня, мне-то всё равно, девушка передо мной или мужик."
        bt "Да-да, конечно, молодец. Знаю, вижу, ты у нас такой сильный и смелый, горы одним взглядом двигаешь."
        bt "И почему бы тебе не собрать всю свою смелость в кулачок и не двинуть отсюда куда подальше?"
        cork unhappy "Ох, я бы двинул, но не подальше отсюда."
        bt neutral "Давай-давай. Двигайся уже, очередь занимаешь."
        cork neutral "Больно надо..."

    hide bartender neutral with dissolve
    cork neutral "Что ж, ладно. Другого выбора у меня нет, пойду пройдусь по улице. Может, в баню загляну, помоюсь наконец-то..."
    jump bath
    return

label concert:
    
    scene bg concert with fade

    "Вы заходите в большой зал, который здесь по какой-то причине называется концертным."
    "Хотя, нет, причина ясна, здесь чаще всего проходят различные мероприятия, праздники, здесь выступают артисты, певцы, иногда ставятся целые театральные постановки."
    "Было бы замечательно, если бы сюда ещё кто-то ходил. По крайней мере, местное население вполне успешно клюёт на уловку в виде бесплатного спиртного, или же алкоголя."
    "Поэтому все, кому не лень, пользуются этим и приносят с собой как минимум несколько ящиков, либо же за свой счёт заказывают открытие бара неподалёку."
    "К слову, как раз там вы и были несколькими минутами ранее."
    "Тем не менее, концертный зал не был таким большим, как может показаться на первый взгляд. Здесь могло поместиться максимум человек 150, плюс ещё какое-то количество, но только стоя."
    "В итоге около двухсот человек в одном помещении, которые пришли скорее за выпивкой, чем за культурным просвещением."
    "Но, в основном, так и набиралась где-то сотня человек на мероприятии, но никто особо не переживал по этому поводу. Деньги несли и хорошо."

    show cork happy at right2 with easeinright
    cork "Фух, наконец-то хоть какой-то отдых от всей этой беготни. Я надеюсь, хотя бы здесь не придётся выпрашивать у всех подряд бутылку, деньги-то все на билет ушли."
    cork puzzled "Хотя..."
    pause 2.0
    cork happy "Меня ничего не остановит! Вот возьму и просто украду, и кто мне что скажет?"
    cork "А, смотри-ка, я за это всё время практически успел прийти в норму, и голова почти не болит."
    cork "Круто! Итак, где там моя бутылочка..."
    speaker "По техническим причинам наступление концерта задерживается ещё на полчаса. Приносим свои извинения."
    cork "Да и фиг с ним! Первый раз, что ли? Подождём!"
    cork neutral "Тем более, что сейчас можно попытаться найти новых собутыльников. Старые, увы, не выдержали попадания в печень такого большого количества..."
    cork happy "Свинца."
    cork neutral "Надеюсь, в этот раз никто меня бесить не будет и всё обойдётся без кровопролития."
    cork "Так ведь?"
    pause 3.0
    cork "Что ж, похоже, ничего страшного пока не происходит. Пойду людей искать, скучно одному."
    hide cork neutral with easeoutleft

    pause 5.0
    "Действительно, сцена выглядела совершенно пустой, и на ней пока что никто не собирался выступать. Видно, действительно произошла какая-то задержка."
    "Опаздывающий народ потихоньку подтягивался и дальше заполнял и без того почти полный зал. Все ждали и надеялись, ведь концерты здесь давали достаточно редко, особенно рок."
    "Шум людских голосов постепенно становился громче, ведь в толпе росло недовольство внеплановой задержкой артистов. Никто здесь, похоже, не умел ждать, а ведь прошло всего минут 5 после объявления."
    "В какой-то момент некоторые личности пытались пробраться в помещения, где предположительно находились сами музыканты, однако их моментально выпинывала оттуда вооружённая охрана."
    "В остальном всё было довольно мирно."
    "Но это только до тех пор, пока внезапно не прогремел оглушительный взрыв, который выбил одну из дверей зала."
    stop music fadeout 1
    pause 2.0
    "Эй, где звук?"
    pause 2.0
    play sound boom
    show bear gun at left2 with hpunch
    pause 1.0
    stop music fadeout 1
    play music battle fadein 1
    bear "{size=+10}{b}Всем лежать! Работают \"Медведи\"™!{/b}{/size}"
    bear "С этого момента все, кто находится в этом зале наши заложники."
    bear "Вы {b}обязаны{/b} подчиняться нашим новым законам и правилам, которые действуют с данного момента по всей территории города."
    bear "Не пытайтесь покинуть помещение или оказывать сопротивление, иначе за это вы лишитесь жизни!"
    bear "А, возможно, и самого дорогого для вас пива!"
    show cork happy at right2 with easeinright
    cork "Так-так, наконец-то я вернулся. Интересно, тут уже началось? Я слышал какой-то шум."
    bear "Мы уже взяли под контроль этот небольшой городок, и в наших планах весь мир!"
    bear "Наша идеология будет куда лучше остальных религий и других идеологий. Потому что мы точно знаем, как правильно! И как неправильно тоже."
    bear "Мы создадим великое государство, аналогов в мире которому не будет нигде и никогда! Мы станем великими!"
    cork neutral "Чёрт возьми, что несёт этот чувак? Он бредит? Почему у него с собой оружие?"
    bear "Мы мессии для вашего гнилого захолустья. Под нашим руководством жизнь будет куда лучше, чем когда-либо была!"
    bear "Примите нашу власть, иначе нам придётся применить силу."
    cork "Мне это начинает надоедать..."
    menu: 
        "Что решишь сделать?"
        "Поставить фанатиков на место":
            cork unhappy "{b}{size=+10}Эй{/size}! Вы там!{/b} Вы что тут устроили?"
            bear "Э-э.. чувак, ты про что?"
            cork "Я сюда на концерт пришёл, а не выслушивать свидетелей Медвеговы!"
            bear "Ну, если не нравится - можешь уходить. Никто не держит. Ой, точнее, держит, ха-ха! Никуда ты не уйдёшь, сиди и слушай, блоха рыжая."
            cork angry "А вот за блоху ты у меня ответишь! И за всё, что было ещё тогда!"
            bear "Тогда.. Забавно, а я-то думал, что это мы за тобой пришли. По крайней мере, оружие есть только у нас!"
            bear "Можешь даже не сопротивляться, твой череп всё равно недостаточно крепкий, чтобы остановить пулю."
            cork unhappy "Зато твои извилины недостаточно умные, чтобы понять, что ты несёшь бред собачий!"
            bear "Хах, и это мне говорит человек, который не может сложить \"два плюс два\" и понять, кто стоит за всем тем, что творится в этом городе."
            cork "Ну, теперь уж это очевидно! И настолько тупо, что даже гениально, я поражён. И в чём же состоял ваш план, маэстро?"
            bear "Нет, мы не такие тупые, как ты. Здесь людьми движет только желание влить в себя побольше алкашки, чтобы отключиться от этого говна, в котором они живут. А дальше, сам знаешь, похмелье и прочая чепуха."
            bear "Все ломятся покупать какие-то препараты и, в том числе, пиво. Мы накрыли все поставки, которые только могли, и полностью отрезали население от любых средств. Никаких лекарств, никакого пива."
            bear "Всё будет только у нас, и, внезапно, мы приходим на помощь обездоленному народу! Все любят нас! Все поклоняются нам! Вся власть {b}только{/b} нам! Аве Медведи, теперь мы будем новыми главарями этого места!"
            bear "А тех, кто не согласен, пустим на фарш. Уяснил?"
            cork puzzled "Ага. Всё ещё звучит как бред."
            bear "Но работает! И будет работать! Ты уже в нашей власти, и встретишь тут свой конец!"
            cork unhappy "Ну уж извините, мой конец теперь всегда при мне, встречать, а тем более терять, я никого не намерен."
            bear "Раз такой смелый, то можешь попробовать что-то сделать. Отсюда ты не выйдешь, нам ты ничего не сделаешь. Абсолютно беспомощным чувствовать себя не особо приятно, правда, Штопор?"
            cork gun "Заткниксь, гнида! Лови, подарок лично от меня!"
            stop music fadeout 1
            pause 1.0
            scene black
            pause 0.5
            play sound gun
            pause 5.0
            scene bg concert with dissolve
            play music normal fadein 1
            show cork unhappy at right2 with dissolve 
            cork "Вот же твари... Надо же было связаться с ними 15 лет назад, так и не отвязались с тех пор. Ну ничего, сейчас они хорошенько отожгли на концерте, теперь одной проблемой из прошлого меньше."
            speaker "Уважаемые гости, рады сообщить вам, что проведение концерта в силе! Прошу подождать пару минут, пока сцену освободят, и выступление сразу же начнётся!"
            speaker "А в качестве моральной компенсации каждый может взять себе по две бутылки пива из нашего холодильника."
            cork happy "Господи, ну наконец-то!"
            stop music fadeout 1
            play music menu_ fadein 1
            window hide
            scene good ending with fade
            pause 2.0
            "Штопор получил всё, что хотел, браво."
            #good ending
        "Попытаться договориться":
            cork neutral "Хей, чуваки, кончайте балаболить! Что вы от нас-то хотите конкретно?"
            bear "От вас? Ну, от местных дурачков ничего, кроме как полного покорения и подчинения нам. Кажется, они абсолютно не против такого."
            bear "А вот от тебя... Ха-ха, это мы сейчас расскажем! Во-первых, мы пришли напомнить тебе за твои прошлые грешки. Помнишь, ещё тогда, 15 лет назад? Так вот, вспоминай."
            bear "Когда-то ты был в наших рядах, участвовал в некоторых тёмных делишках, всё такое. Ничего необычного, всё то, чем занимается обычный житель. Но недолго продержался, всего пару лет или даже меньше."
            bear "Но пришли мы не за то, чтобы за выход из группировки наказать. Ты сдал мусорам двоих из нас! Они до сих пор на зоне сидят, платья женские штопают за копейки. Один даже думает вернуться к нормальной жизни, немыслимо!"
            cork puzzled "Ого, а я думал, что мужские платья шьют, надо же в чём-то им там ходить."
            bear "{b}Молчать!{/b} Ты не знаешь, о чём говоришь, идиот! Сам никогда не был за решёткой, ещё и шутки шутишь!"
            cork neutral "Ну... в отличие от тебя я там был. И прекрасно знаю, как оттуда сбежать. Поэтому предлагаю свою помощь в том, чтобы вызволить твоих ребят из гнилого места."
            bear "Ты шутишь, что ли? Какая ещё помощь от предателя?"
            cork happy "Нет-нет, ни в коем разе не шучу!"
            cork neutral "Давайте на чистоту, я вытаскиваю этих двоих из тюряги, а вы за это не трогаете ни меня, ни мой дом. Договорились? А на остальных мне плевать, делайте с ними что хотите."
            bear "Хм, мне надо обдумать это предложение... Секунду."
            hide bear gun with moveoutleft
            pause 1.0
            show bear beer at left2 with moveinleft
            bear "Что ж, ладно, мы согласны. Но учти, будешь выделываться - пулю в лоб и до свидания. Понял?"
            cork happy "Так точно, сэр! А эта бутылка мне как аванс?"
            bear "Нет, не тебе, свали уже отсюда."
            cork neutral "Что ж, ладно. Придётся приниматься за работу."
            hide cork with easeoutleft
            pause 1.5
            stop music fadeout 1
            play music battle fadein 1
            window hide
            scene neutral ending with fade
            pause 2.0
            "Штопор обратно присоединился к Медведям™."
    
    return

label bath:

    scene bg bathhouse with fade

    "Вы заходите в баню. Всем известное в этом городе место, ведь сюда часто приходят те, у кого нет возможности помыться у себя дома, потому что горячую воду опять отключили на 2 недели." 
    "Либо же потому, что истосковались по виду женской фигуры. Да, разделения по полам тут не было, был только один общий зал. Что поделать, зато помещение было огромное и вместе с тем душное и горячее."
    "Всё пропахло потными телами разных людей, и в различных местах можно было уловить щекотливые нотки цветочных духов, либо же почувствовать аромат двухнедельного запоя."
    "Но все были здесь довольны, потому что, говорят, будто вода здесь обладала какими-то совсем магическими свойствами. Начиная с того, что избавляла от головных болей, заканчивая тем, что омолаживала на целых 10, а то и 20 лет."
    "Но по некоторым пропитым рожам и не скажешь, а возможно на алкоголиков просто не действует эта магия."

    show cork bath_happy at right2 with easeinright

    cork bath_happy "Та-ак, банька-парилка! Наконец-то я сюда добрался. Сегодня, так уж и быть, обойдёмся без пива, но зато хотя бы помоюсь. Раз в неделю-то уж надо."
    cork "Итак, что тут у нас интересного есть? Ага, значит, тут всё общее, без разделения на женское и мужское. Смешались и люди, и кони... Что ж, по крайней мере, будет на что посмотреть. Девушки, ждите меня!"
    cork bath_neutral "О, смотрите-ка, и сразу приятный взору вид. Интересно, как зовут эту дам... А-а, нет, забудьте..."
    
    show collegue bath_neutral at left2 with easeinleft
    
    col "Чего тебе, идиот?"
    cork bath_puzzled "Кхм. Привет, Линда. Или Илиада? А-а, нет, подождите, точно, вспомнил. Мандолина! Ух, у тебя столько разных образов и имён было, сразу и не вспомнишь все."
    col "Зато ты как был тупым, так и остался. И воняешь, как обычно. Чего тебе, остолоп?"
    cork bath_neutral "Ну-с, со вторым я как раз решаю проблемы. Вроде уже как пару лет не виделись, а ты всё такая же сварливая бабёнка. Я даже сначала и не узнал, пока ты не повернулась. Сплошная пластика налицо."
    col bath_unhappy "Ты совсем глупый или да? Я тебя ещё раз спрашиваю: тебе чего от меня надо? Я повторяться не люблю."
    cork bath_puzzled "Ну, как бы сказать.. да, в принципе, ничего и не надо. Просто встретил бывшую коллегу в людном месте, почему бы и не поболтать немного?"
    col "Слышь, умный! Тебя сейчас мой парень отсюда вышвырнет за такое."
    cork bath_happy "За какое такое? Я ж вроде и не сделал ничего. И неужели у тебя парень появился? Опять. А куда ты остальную сотню дела? О ужас! Ты их съела?!"
    col bath_neutral "Если это была шутка, то я советую тебе больше не шутить. За такие шутки в зубах бывают промешутки."
    cork "Хах, я бы посмотрел на того, кто попытался бы мне сделать какие-то \"промешутки\" в зубах."
    col bath_unhappy "Ну и вали отсюда тогда, быдло! Хоть на меня пялиться не будешь, мне от одного твоего взгляда грязно становится."
    cork bath_puzzled "Насчёт взгляда, кстати... Почему ты тут в купальнике? Вроде ж не курорт, да и не сезон, с чего это вдруг?"
    col bath_unhappy "Не твоё дело, псина сутулая. Рот бы свой закрыл, а то воняешь."
    cork bath_neutral "Повторяешься. Да и... Выглядишь, честно говоря, не очень. Смотреть не на что."
    col "Может тогда и пялиться на меня не будешь, быдлятина?"
    cork "Да-да, было уже, было."

    menu:
        "Слушай, тебе не надоело терпеть эти оскорбления?"
        "Погодите-ка, и правда, эта мадам как-то сильно перегибает палку!":
            cork bath_unhappy "Слушай, а тебе вот так вот не надоело вставлять оскорбления буквально в каждой фразе?"
            col bath_neutral "А что, мозгов понять слова не хватает?"
            cork bath_neutral "Нет, я просто поражаюсь, насколько ограниченный словарный запас у такой курицы, как у тебя."
            col bath_unhappy "Ах!! Это я курица?!"
            cork bath_happy "Ну не я ж. Или что, скажешь теперь, что я остолоп?"
            col "Нет, тупица!"
            cork "Ого-го, ты ещё знаешь, как формы слов образуются. Ну-ка, скажи ещё что-нибудь."
            col "Идиот!"
            cork bath_neutral "О, а вот это уже что-то новенькое. Но мне всё равно. Больно громко ты раскудахталась, смотри, не напрягайся сильно, а то вдруг яйцо у тебя случайно вывалится."
            col bath_neutral "Заткнись уже! Я иду к своему парню."
            cork bath_unhappy "Ты мне не угрожай, мадам. Я твоего парня в труху сотру, и тебя на куриный бульон оставлю, а тебе я бы посоветовал помалкивать, пока не случилось чего-нибудь трагичного."
            col "Сам не знаешь, что несёшь. Иди отсюда, пока я добрая, глупый."
            cork bath_happy "Это ты добрая? Ха-ха, да по сравнению с тобой я уже должен был быть причислен к лику святых. Ты ж змеюка подколодная, мне постоянно отказывала в съёмках совместных."
            cork bath_neutral "Из-за тебя наши режиссёры в окно прыгали, чтоб тебе только угодить. Но тебе всё мало было! И мозгов мало, в том числе!"
            col "Ой-ой, посмотрите-ка, ущемился малыш. Что, промежность колется, да не можется? Ха-ха, такой жалкий."
            cork bath_unhappy "Ну, сейчас меня ничего не останавливает, кроме общественного осуждения, но и на общество мне в целом насрать. Тебе кранты, крошка."
            col bath_unhappy "Ага, щ-щас. Бывай, тупица. Надоело твою вонь терпеть."
            hide collegue bath_unhappy with easeoutleft
            cork "Вот же блин, и где мне её в толпе искать? Большие сиськи не у неё одной, но чтобы настолько много силикона в одном человеке... это ещё постараться надо."
            cork bath_neutral "Чёрт, мелочь упрямая. Вот погоди, когда-нибудь мы снова встретимся, и тогда я..."
            jump room #way to bad ending 

        "Да не, всё нормально, это даже весело":
            cork bath_happy "Слушай, а ты вообще когда-нибудь читала что-то вроде книги? Не то, чтобы я сам как-то этим сильно увлекался, но могу осилить хотя бы что-то сложнее состава освежителя воздуха."
            col bath_neutral "Не твоё собачье дело."
            cork "Ну что ты, уже фантазия кончилась на оригинальные ответы? Давай-давай, не надо останавливаться."
            col "Иди отсюда."
            cork bath_puzzled "Не пойду. И что тогда?"
            col bath_unhappy "Тогда тебе же хуже будет, глупый."
            cork bath_neutral "И где же мне хуже станет? Ну, хотя, да, мне уже становится немного скучно."
            col bath_neutral "Ну вот и иди развлеки себя сам, доставь себе удовольствие, нечего к взрослым людям лезть."
            cork bath_happy "Хах, это ты-то взрослая? А тебе напомнить, как ты с десяток парней променяла просто из-за того, что они тебе какой-то не такой подарок дарили на свиданиях?"
            cork bath_puzzled "Кажется.. это был плюшевый мишка, так? Сколько их у тебя вообще, любительница медведей?"
            col "Пф, нашёл что вспомнить. Ты бы лучше за собой следил. Нюни распускаешь каждый раз, когда бутылки под рукой не оказывается. Что, малышу сосочки не досталось, да?"
            cork bath_neutral "Да чья бы корова мычала! Ты бы лучше вспомнила, как на одном корпоративе целый час убивалась о том, что тебе дали вино красное."
            cork "И даже не какое-то там белое полусладкое полужирное с запахом роз, ещё и ножкой топала! А потом сама же втихую выпила всю бутылку и начала на столе танцевать."
            col bath_unhappy "Между прочим, я не просто так выбираю себе вино! От того, что я люблю, я не пьянею, а просто себе настроение поднимаю и веду себя более свободно."
            cork bath_happy "Ага, куда ж ещё свободнее, чем сейчас. У тебя там уже, наверное, ветер гуляет. В голове. Зато свободно, и ничего не мешает."
            col bath_neutral "А сам-то. После того, как уволился, поплёлся домой пьяный, а потом несколько дней бегал с голой жопой и искал свой потерянный..."
            cork bath_unhappy "Так! А вот об этом не надо! Всё-таки это уже личная тема... И-и тебя это вообще не касается!"
            col "Ладно-ладно, девочка моя, не буду. Сильно испугался?"
            cork "Это уже тебя надо будет спрашивать, когда я тебе за твои шутки проблем добавлю."
            col bath_unhappy "Эй-эй, ты на что намекаешь, тупица?"
            cork "Тупица тут уж точно не я, раз не понимаешь намёков. Готовься..."
            col "Уйди от меня!"
            cork bath_puzzled "А не то что? Парень твой придёт? Ну пусть приходит, я посмотрю, посмеюсь хоть." 
            col bath_neutral "А вот и не угадал! Придут люди в масках и сделают из тебя решето. Уходи пока живой ещё."
            cork bath_neutral "Хах, что это за цаца, посмотрите на неё. Ты себе и телохранителей наняла? Да тут уже... И охранять то нечего, это не тело, а силиконовый контейнер."
            col bath_unhappy "Молчи! Или увидишь свой конец. Отдельно, в баночке, заспиртованный."
            cork bath_unhappy "Ну уж нет, второй потери я не перенесу. И хватит уже об этом! Про какие маски ты вообще говорила? Надеюсь, не медицинские."
            col bath_neutral "А что, боишься? Короче, если увидишь на улице людей в масках медведей ты труп."
            cork bath_neutral "Хах, спасибо за предупреждение. Буду носить с собой ствол, и не только в штанах."
            col "Как будто тебе такая мелочь как-то поможет."
            cork bath_unhappy "Мелочь? Ну уж извините! В этом городе я держу неоспоримый рекорд."
            col "Иди уже отсюда, идиот."
            сork bath_neutral "А вот сама бы и пошла отсюда, места тут предостаточно."
            col bath_unhappy "Надеюсь, ты подохнешь, тупица."
            cork bath_happy "Взаимно!"
            hide collegue bath_unhappy with easeoutleft
            pause 1.0
            show cork bath_neutral at right2 with dissolve
            pause 1.0
            cork bath_unhappy "{p=1.0}Чёрт, медведи. Что-то знакомое. Но я не могу вспомнить..."
            cork bath_puzzled "Почему-то я вспоминаю что-то только из прошлого. Маша и Медведь? Нет, бред, какие тут ещё сказки. Они террористы? Какая-то группировка? Возможно, возможно..."
            cork bath_unhappy "Твою ж мать, эта сволочь уже ушла, и не просить её, о чём она. Как-то странно это всё складывается. Точнее, вообще не складывается."
            cork bath_neutral "Сначала поломанный генератор с какой-то горящей шерстью, потом пропажа пива везде, потом ещё и эта..."
            cork bath_puzzled "Не могу понять, слишком сложно. Если это действительно те медведи, о которых я думаю, то мне {b}крупно{/b} не повезло. Они ж мне за все мои прошлые грехи напомнят, и сверху ещё добавят."
            cork bath_neutral "{p=1.0}... Кажется, это именно они."
            cork bath_unhappy "Что ж, придётся пояснять за базар, правда уже лет 15 прошло. Может быть, они забыли уже? Надеюсь, что так."
            cork bath_neutral "Ладно, без разницы, я сюда не за этим пришёл. А сейчас мыться и домой."
            jump home #way to neutral or bad ending

    return

label home:

    scene bg toilet with fade
    show cork happy at right2 with easeinright

    cork "Ах, наконец-то, дом милый дом. По крайней мере я помню, почему он милый. Потому что мне срочно надо отлить. Вроде пива не пил, а всё равно хочется."
    cork "Вся эта беготня по городу меня так утомила, что я аж про себя самого забыл, и что у меня какое-то похмелье было. Ну-ка, где оно там."

    stop music fadeout 1
    play sound boom
    scene toilet2 with vpunch
    pause 1.0
    play music battle fadein 1
    show cork drunk at right2 with dissolve

    cork unhappy "Чт.. что за чёрт?! Почему в моём унитазе была грёбаная взрывчатка?! Тьфу! Какой шутник это сделал?! И ведь в дом пробрался, главное, падла! Сейчас кто-то точно будет огребать!"
    cork "Ух, если найду этих ублюдков, точно из них всё дерьмо выбью! Захотел расслабиться, понимаешь, после такого тяжёлого дня, а тут на тебе!"

    pause 1.0
    scene bg backyard with fade
    show cork angry at right2 with moveinright

    cork "Эй, кто тут!"
    show bear beer at left2 with easeinleft
    bear "Не ори, девочка."
    cork "Какая я тебе девочка?! Слепой, что ли?"
    bear "Ага, а то мы не знаем, персик ты наш."
    cork unhappy "Н-ненавижу персики... Неважно! Проваливайте отсюда, пока живы!"
    bear "Чувак, ты нарываешься! Щас пулю в лоб словишь."
    cork "Ты думаешь меня этим напугать? Сосунок, да ты даже оружие правильно держать не умеешь! Хорошо, что я от вас свалил в своё время!"
    bear "Ничего страшного, Штопор, от нас далеко не уйдёшь. Как видишь, мы тебя нашли. По твоим же фотографиям."
    cork neutral "Так и знал, что вы пидорасы."
    bear "Заткнись! Это твой конец."
    cork unhappy "И чего ради вы всё это устроили? Это же вы взорвали тот генератор в магазине! А нахера?!"
    bear "А потому что! Мы пришли не только тебе дуло в задницу вставить, но и здесь власть свою установить."
    cork puzzled "Ебанутые..."
    bear gun "Молчи, несчастный! Мы установим здесь свои порядки."
    cork neutral "И что, каждого перестрелять задумали?"
    bear "Нет, мы не такие тупые, как ты. Здесь людьми движет только желание влить в себя побольше алкашки, чтобы отключиться от этого говна, в котором они живут. А дальше, сам знаешь, похмелье и прочая чепуха."
    bear "Все ломятся покупать какие-то препараты и, в том числе, пиво. Мы накрыли все поставки, которые только могли, и полностью отрезали население от любых средств. Никаких лекарств, никакого пива."
    bear "Всё будет только у нас, и, внезапно, мы приходим на помощь обездоленному народу! Все любят нас! Все поклоняются нам! Вся власть {b}только{/b} нам! Аве Медведи, теперь мы будем новыми главарями этого места!"
    bear "А тех, кто не согласен, пустим на фарш. Уяснил?"
    cork unhappy "Понял. Понял, что вы совсем кукухой поехали на старости лет. Это полнейший бред! Вы сами-то себя слышали?"
    bear "Всё прекрасно работает, неужто ты сам не замечал?"
    cork "Замечал, ещё как. Но то, что вы полные идиоты и умственно отсталые - это факт!"

    menu: 
        "Момент истины: застрелить террористов?"
        "Конечно! Заслужили.":
            cork gun "Ловите, ублюдки, подарок по старой дружбе!"
            stop music fadeout 1
            pause 1.0
            scene black
            pause 1.0
            play sound gun
            pause 5.0
            play music normal fadein 1
            scene bg backyard with dissolve
            cork unhappy "Фух... Тс, чёрт, теперь сиди чини дом после них. Идиоты. Полные идиоты."
            cork neutral "Зато одной проблемой из прошлого меньше. Возможно и пиво появится скоро."
            stop music fadeout 1
            play music menu_ fadein 1
            window hide
            scene neutral ending with fade
            pause 2.0
            "Остался без пива, зато живой."
            #neutral ending

        "Я бы подумал над тем, чтобы к ним присоединиться...":
            cork neutral "..К счастью, я такой же. Или к сожалению, я ещё не понял. Неважно. Я же раньше у вас в банде состоял, да?"
            cork happy "Чего вам стоит меня обратно принять, а? Забейте на взорванный дом, его починить можно. А вот стрельбу, думаю, я не переживу..."
            bear "Ты совсем конченный? Какой ещё принять обратно? Ты только что нас оскорблениями обкладывал!"
            cork puzzled "Ну, я.. э-э, перепугался просто! Ничего личного, я не хотел. Просто всё перед моим лицом взорвалось, я в замешательстве, сами понимаете."
            bear "Нет. Не понимаем. Пацаны, кончайте с ним."
            stop music fadeout 1
            pause 1.0
            scene black
            pause 1.0
            play sound gun
            pause 3.0
            stop music fadeout 1
            play music menu_ fadein 1
            window hide
            scene neutral ending with fade
            pause 2.0
            "Теперь Штопор мёртв."
            #bad ending, corkscrew is dead

    return

label room:

    show old_ unhappy at left2 with moveinleft
    old_ "ТАК. Чт-то тут п-происходит?"
    cork puzzled "Дед, а ты то откуда здесь вылез? Вроде не магазин, акции на гречку нет."
    old_ "М-молчи, ирод! Ты п-пошто девчульку мою пуг-гаешь?"
    cork "Погоди, какую ещё девчульку? Не знаю я никаких девочек, я таким не интересуюсь. Если надо, то иди В.П.И. ..."
    old_ "Ты! Идёшь с-со мной!"
    cork neutral "Куда? На кладбище сразу? Ну ладно, помогу дедушке через дорогу перейти."

    scene bg room with dissolve
    show old_ neutral at left2 with easeinleft
    show cork neutral at right2 with easeinright

    old_ neutral "Т-так, молод-дой человек!"
    cork happy "Я вас слушаю, хоть я уже и не такой молодой."
    old_ unhappy "Не п-перебивай, когд-да с тобой старшие г-говорят!"
    cork neutral "Молчу-молчу. А вы, дяденька, кто вообще? И зачем вы привели меня в такие роскошные апартаменты? Ваша усыпальня, то есть, спальня?"
    old_ neutral "А в-вот сейч-час и узнаешь."
    cork "Да ладно, и что ты со мной сделаешь сейчас? Учти, я не плотник, гроб тебе из кровати сколотить не смогу."
    old_ unhappy "Себе если т-только. Ложись н-на кровать, живо!"
    cork puzzled "Это ещё зачем? Это какой-то агрессивный маркетинг хостела? У меня денег с собой нет, только полотенце это."
    old_ neutral "И полот-тенце снимай."
    cork unhappy "Дед, ты меня настораживаешь. У меня ж под ним ничего нет... чёрт, ещё и дверь закрыта, никак не поддаётся."
    old_ "Снимай и л-ложись!"
    cork neutral "Хорошо, хорошо, ты только не напрягайся, а то мало ли, сердце не выдержит. Мне хоть заплатят за это?"
    old_ unhappy "Если не сд-делаешь, ты своей жизнью зап-платишь! Я здесь очень-оч-чень важный человек, между прочим! Или т-тебе кранты, или делаешь то, ч-что я говорю."
    cork "...Чёрт, походу он не шутит. Что тебе надо, старый? Я ректальным массажем не занимаюсь."
    old_ neutral "Зато я зан-нимаюсь, кхе-кхе."
    cork unhappy "Чт.. О-о, нет, только не это..."
    old_ "Зато уйдёшь от-тсюда живым и, м-может быть, здоровым."
    cork puzzled "А если я вдруг выломаю дверь и выбегу отсюда? Я не думаю, что ты сможешь меня догнать."
    old_ "Ну и глуп-пый же ты! Тебя мои м-медвежатки накроют. Тю-тю из-з пушки и н-нет тебя."
    cork "Постой, дед, какие ещё медвежатки? Ты о чём вообще? Постой-ка, а не ты ли это тот самый глава бандитской группировки?"
    cork "Ну, которые как клоуны раньше наряжались в костюмы и бегали по всему городу."
    old_ "Да-да, кон-нечно, милок. Но сейчас не об эт-том."
    cork neutral "Погоди, как это не об этом? Я же когда-то был..! Ай, нет, молчу-молчу. Не сейчас, так не сейчас. Говори, что ты там хотел."
    old_ "Так вот-т. Согласишься со мной поуч-частвовать в процессе, я тебя и отпущу. А если н-нет сам виноват."
    cork unhappy "Похоже, выбора у меня нет... Ладно уж, давай, что ты там хотел..." 
    stop music fadeout 1
    play music menu_ fadein 1
    window hide
    scene bad ending with fade
    pause 2.0
    "Кажется, произошло что-то очень неприятное."
    #bad ending
    return